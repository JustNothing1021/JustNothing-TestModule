

plugins {
    id 'com.android.application'
}


android {
    namespace 'com.justnothing.testmodule'
    compileSdkVersion 36

    defaultConfig {
        applicationId "com.justnothing.testmodule"
        minSdk 26
        targetSdk 36
        versionCode 43
        versionName "0.4.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86_64', 'x86'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    configurations {
        all*.exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
    }
    buildToolsVersion '36.1.0'
    ndkVersion '29.0.14206865'
}

dependencies {
    implementation libs.androidx.appcompat
    implementation libs.material.v1110
    implementation libs.androidx.constraintlayout.v221
    implementation libs.androidx.navigation.fragment.v277
    implementation libs.androidx.navigation.ui.v277
    implementation libs.firebase.crashlytics.buildtools
    implementation libs.bsh
    implementation libs.hutool.all
    implementation libs.gson
    implementation libs.androidsvg.aar
    implementation libs.asm
    implementation libs.asm.tree
    implementation libs.asm.util
    implementation libs.cfr
    implementation files("libs/NCW-Logger-1.0.3-hotfix.jar")

    compileOnly libs.xposed.api

    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit.v130
    androidTestImplementation libs.androidx.espresso.core.v370
}


configurations.configureEach {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'androidx.lifecycle') {
            details.useVersion "2.7.0"
        }
    }
}

def clientSourceDir = layout.buildDirectory.dir('libs/source')
def clientJarDir = layout.buildDirectory.dir('libs/client')


tasks.register('createClientSourceJar', Jar) {
    group = 'build'
    description = '创建客户端源代码的Jar (还不能直接用, app_process认不出来)'
    archiveBaseName = 'methods-client'
    destinationDirectory = clientSourceDir

    def javaClassesDir = project.objects.directoryProperty()
    javaClassesDir.set(tasks.named('compileDebugJavaWithJavac').flatMap { it.destinationDirectory })

    from(javaClassesDir) {
        include 'com/justnothing/methodsclient/**'
        include 'com/justnothing/testmodule/**'
        exclude 'com/justnothing/testmodule/ui'     // 因为没必要其实
        exclude 'com/justnothing/testmodule/databinding'
        exclude '**/*.java'
    }

    manifest {
        attributes(
                'Main-Class': 'com.justnothing.methodsclient.StreamClient',
                'Created-By': "Gradle ${gradle.gradleVersion}"
        )
    }

    dependsOn tasks.named('compileDebugJavaWithJavac')
}

tasks.register('dexifyClientSourceJar') {
    group = 'build'
    description = '将客户端源代码Jar转换为dex'
    dependsOn tasks.named('createClientSourceJar')
    doLast {
        def inputJarFile = tasks.named('createClientSourceJar').get().archiveFile.get().asFile
        def outputDir = new File(clientSourceDir.get().toString())
        outputDir.mkdirs()
        for (File oldFile : outputDir.listFiles({file -> file.name.endsWith(".dex")} as FileFilter))
            oldFile.delete()
        logger.lifecycle("开始将 ${inputJarFile.absolutePath} 转换为dex格式...")
        logger.lifecycle("输出目录: ${outputDir.absolutePath}")
        def androidSdkDir = android.sdkDirectory
        def xposedLibPath = "${project.projectDir}/libs/XposedBridgeApi.jar"
        def androidLibPath = "${project.projectDir}/libs/android.jar" // 没招了
        if (androidSdkDir != null) {
            def d8BatTool = new File(androidSdkDir, "build-tools/${android.buildToolsVersion}/d8.bat")
            def d8Tool = new File(androidSdkDir, "build-tools/${android.buildToolsVersion}/d8")

            def d8Executable
            if (d8BatTool.exists()) {
                d8Executable = d8BatTool.absolutePath
                logger.lifecycle("使用d8.bat: ${d8Executable}")
            } else if (d8Tool.exists()) {
                d8Executable = d8Tool.absolutePath
                logger.lifecycle("使用d8: ${d8Executable}")
            } else {
                throw new GradleException("无法找到d8，请检查Android SDK build-tools目录")
            }
            ExecOutput result = null
            try {
                def command = [d8Executable, '--release', '--lib', androidLibPath, '--lib', xposedLibPath, inputJarFile.absolutePath, '--output', outputDir.absolutePath] as String[]
                def process = new ProcessBuilder(command)
                        .directory(project.projectDir)
                        .redirectErrorStream(false)  // 分开stdout和stderr
                        .start()
                
                def stdout = new StringBuilder()
                def stderr = new StringBuilder()
                
                Thread.start { process.inputStream.eachLine { stdout.append(it).append('\n') } }
                Thread.start { process.errorStream.eachLine { stderr.append(it).append('\n') } }
                
                def exitCode = process.waitFor()
                
                logger.lifecycle("d8执行完成，退出码: " + exitCode)
                if (stdout.length() > 0) {
                    logger.lifecycle("stdout: " + stdout.toString().trim())
                }
                if (stderr.length() > 0) {
                    logger.lifecycle("stderr: " + stderr.toString().trim())
                }
                
                if (exitCode != 0) {
                    throw new GradleException("d8执行失败，退出码: " + exitCode)
                }
            } catch (Exception e) {
                logger.lifecycle("d8命令执行失败，错误信息: " + e.message)
                logger.lifecycle("执行的d8命令: " + d8Executable + " --release --lib " + androidLibPath + " --lib " + xposedLibPath + " " + inputJarFile.absolutePath + " --output " + outputDir.absolutePath)
                throw e
            }
        } else {
            throw new GradleException("无法找到Android SDK路径")
        }

        def dexFiles = outputDir.listFiles({ file -> file.name.endsWith('.dex') } as FileFilter)
        if (dexFiles && dexFiles.length > 0) {
            logger.lifecycle("转换成功, 文件列表: ")
            dexFiles.each { file ->
                logger.lifecycle("${file.absolutePath} (大小${file.length()} bytes)")
            }
        } else {
            throw new GradleException("未生成预期的dex文件")
        }
    }
}

tasks.register('createClientJar', Jar) {
    group = 'build'
    description = '创建methods的Java客户端Jar文件'
    archiveBaseName = 'methods-client'
    destinationDirectory = clientJarDir

    dependsOn tasks.named('dexifyClientSourceJar')

    from (clientSourceDir) {
        include '*.dex'
    }

    manifest {
        attributes(
                'Dex-Main-Class': 'com.justnothing.methodsclient.StreamClient',
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Time': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        )
    }
}

tasks.register('packageMagiskModule', Task) {
    group = 'build'
    description = '打包Magisk模块'
    
    dependsOn tasks.named('createClientJar')

    def magiskDir = file('build/magisk')
    def systemBinDir = file("${magiskDir}/system/bin")
    def systemFrameworkDir = file("${magiskDir}/system/framework")
    def commonBinaryDir = file("${magiskDir}/common/binary")
    def commonDir = file("${magiskDir}/common")

    doLast {
        delete magiskDir
        magiskDir.mkdirs()
        systemBinDir.mkdirs()
        systemFrameworkDir.mkdirs()
        commonBinaryDir.mkdirs()
        commonDir.mkdirs()

        def moduleProp = file("${magiskDir}/module.prop")
        moduleProp.text = """
id=JustNothing-module
name=啥也不是的模块
version=${android.defaultConfig.versionName}
versionCode=${android.defaultConfig.versionCode}
author=JustNothing1021
description=一个用来调试安卓开发千奇百怪的诡异问题的Xposed模块/命令行工具
"""

        project.copy {
            from file('magisk/install.sh')
            into magiskDir
        }

        project.copy {
            from file('magisk/common/post-fs-data.sh')
            into commonDir
        }

        project.copy {
            from file('magisk/common/service.sh')
            into commonDir
        }

        project.copy {
            from file('build/libs/client/methods-client.jar')
            into systemFrameworkDir
            rename { 'methods-client.jar' }
        }

        project.copy {
            from file('src/main/assets')
            into commonBinaryDir
            include 'arm64-v8a/methods_client_arm64-v8a'
            include 'armeabi-v7a/methods_client_armeabi-v7a'
            include 'x86_64/methods_client_x86_64'
            include 'x86/methods_client_x86'
        }

        project.copy {
            from file('magisk/scripts')
            into systemBinDir
        }
    }
}

tasks.register('createMagiskZip', Zip) {
    group = 'build'
    description = '创建Magisk模块'

    dependsOn tasks.named('packageMagiskModule')

    def magiskDir = file('build/magisk')
    
    archiveFileName = "magisk-module.zip"
    destinationDirectory = file('build/outputs')
    
    from magiskDir
}

afterEvaluate {
    tasks.named('assembleDebug').configure {
        dependsOn tasks.named('createMagiskZip')
    }

    tasks.named('assembleRelease').configure {
        dependsOn tasks.named('createMagiskZip')
    }
}

